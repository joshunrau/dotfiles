#!/usr/bin/env zsh

read -r -d '' usage << EOM
Usage: ${funcstack[1]} [options] <filename>

Arguments:
  filename                the image to resize

Options:
  -h, --help              display help for command
  -o, --output            the output file (if not set, will overwrite the input file)
  -w, --width             the updated width of the file in pixels or as a percentage (e.g., 100, 50%)
EOM

zparseopts -D -E -F - h=help -help=help o:=output -output:=output w:=width -width:=width || exit 1

help=${#help}
if (( $help )); then
  echo $usage
  return
fi

if [[ -z "$1" ]]; then
  echo "Error: must specify target file" && exit 1
fi

output=${output[-1]:-$1}
width=${width[-1]}
if [[ -z "$width" ]]; then
  echo "Error: missing required option '--width'" && exit 1
fi

magick $1 -quality 100 -resize $width $output
